buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.29.0'
  }
}

plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'com.jfrog.artifactory' version '4.29.0'
  id 'maven-publish'
}

repositories {
  gradlePluginPortal()
  mavenCentral()
  google()
}

// distribute the distribution plugin
def pluginVersion = '1.1.2'
def pluginGroupId = 'com.mux.gradle.android'
def pluginArtifactId = 'mux-android-distribution'
def artifactoryCredential = { key ->
  def propsFile = project.rootProject.file('local.properties')
  if (propsFile.exists()) {
    Properties props = new Properties()
    props.load(propsFile.newDataInputStream())
    return props.getProperty(key)
  } else if (System.getenv("ORG_GRADLE_PROJECT_$key") != null) {
    return System.getenv("ORG_GRADLE_PROJECT_$key")
  }
}
def artifactoryUser = artifactoryCredential('artifactory_user')
def artifactoryPassword = artifactoryCredential('artifactory_password')
artifactory {
  contextUrl = "https://muxinc.jfrog.io/artifactory/"
  publish {
    repository {
      repoKey = 'default-maven-release-local'
      username = artifactoryUser
      password = artifactoryPassword
    }
    defaults {
      // the gradle-plugin plugin makes our publications for us
      publications 'ALL_PUBLICATIONS'
    }
  }
}
version pluginVersion
group pluginGroupId
java {
  withSourcesJar()
}
publishing {
  repositories {
    maven {
      url 'https://muxinc.jfrog.io/artifactory/default-maven-release-local'
      credentials {
        username artifactoryUser
        password artifactoryPassword
      }
    }
    maven {
      name = "GitHubPackages"
      url 'https://maven.pkg.github.com/muxinc/mux-android-distribution'
      credentials {
        username System.getenv("GH_USER")
        password System.getenv("TOKEN")
      }
    }
  }
}

gradlePlugin {
  plugins {
    muxAndroidDistribution {
      implementationClass = 'com.mux.gradle.android.MuxDistributionPlugin'
      // Why make it anything else?
      id = "$pluginGroupId.$pluginArtifactId"
      displayName = "Mux Android Distribution Plugin"
      description = "A plugin for publishing multi-variant Android libraries, with support for Artifactory"
    }
  }
}

allprojects {
  tasks.withType(JavaCompile) {
    sourceCompatibility = "11"
    targetCompatibility = "11"
  }
}

dependencies {
  implementation 'org.apache.groovy:groovy:4.0.4'
  implementation 'com.android.tools.build:gradle:7.2.2'
  implementation 'org.jfrog.buildinfo:build-info-extractor-gradle:4.29.0'
  implementation 'org.jetbrains.dokka:dokka-gradle-plugin:1.8.10'
  implementation gradleApi()
  implementation 'com.squareup.okhttp3:okhttp:4.10.0'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
  useJUnitPlatform()
}
